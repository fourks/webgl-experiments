<html>
<head>
  <title>Interactive Gouraud shading and Phong reflection</title>

  <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
                                      
  <script type="text/javascript" src="javascripts/math/gl-matrix-min.js"></script>
  <script type="text/javascript" src="javascripts/webgl/Utils.js"></script>
  <script type="text/javascript" src="javascripts/webgl/Program.js"></script>
  <script type="text/javascript" src="javascripts/webgl/Scene.js"></script>
  <script type="text/javascript" src="javascripts/webgl/Cube.js"></script>
  <script type="text/javascript" src="javascripts/webgl/Camera.js"></script>
  <script type="text/javascript" src="javascripts/webgl/CameraInteractor.js"></script>
  <script type="text/javascript" src="javascripts/webgl/WebGLApp.js"></script>
  <script type="text/javascript" src="javascripts/application.js"></script>
  
  <!-- Vertex shader program -->
  <script id="shader-vs" type="x-shader/x-vertex">  
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;

    uniform mat4 uMVMatrix; 
    uniform mat4 uPMatrix; 
    uniform mat4 uNMatrix;

    uniform float uShininess;   //shininness
    uniform vec3 uLightDirection;  //light direction

    uniform vec4 uLightAmbient;      //light ambient property
    uniform vec4 uLightDiffuse;      //light diffuse property 
    uniform vec4 uLightSpecular;     //light specular property

    uniform vec4 uMaterialAmbient;  //object ambient property
    uniform vec4 uMaterialDiffuse;   //object diffuse property
    uniform vec4 uMaterialSpecular;  //object specular property

    varying vec4 vColor;

    void main(void) {
      //Transformed vertex position
      vec4 vertex = uMVMatrix * vec4(aVertexPosition, 1.0);
    
      //Transformed normal position
      vec3 N = vec3(uNMatrix * vec4(aVertexNormal, 1.0));
    
      //Normalize light to calculate lambertTerm
      vec3 L = normalize(uLightDirection); 
    
      //Lambert's cosine law
      float lambertTerm = dot(N,-L);
    
      //Ambient Term
      vec4 Ia = uLightAmbient * uMaterialAmbient;
    
      //Diffuse Term
      vec4 Id = vec4(0.0,0.0,0.0,1.0);
    
      //Specular Term
      vec4 Is = vec4(0.0,0.0,0.0,1.0);
    
      if(lambertTerm > 0.0) //only if lambertTerm is positive
      {
        Id = uLightDiffuse* uMaterialDiffuse * lambertTerm; //add diffuse term
        
        vec3 eyeVec = -vec3(vertex.xyz);
        vec3 E = normalize(eyeVec);
        vec3 R = reflect(L, N);
        float specular = pow(max(dot(R, E), 0.0), uShininess );
        
        Is = uLightSpecular * uMaterialSpecular * specular; //add specular term
      }
          
      //Final color
      vColor = Ia + Id + Is;
      vColor.a = 1.0;

      //Transformed vertex position
      gl_Position = uPMatrix * vertex;
    }
  </script>

  <!-- Fragment shader program -->
  <script id="shader-fs" type="x-shader/x-fragment">
    #ifdef GL_ES
      precision highp float;
    #endif
    
    varying lowp vec4 vColor;

    void main(void) {
      gl_FragColor = vColor;
    }
  </script>
</head>
<body onload="initialise()">
  <h1>Interactive Gouraud shading and Phong reflection</h1>
  <p>Simple example showing an interactive 3D cube with directional lighting and an orbiting camera.</p>
  
  <canvas id="glcanvas" width="640" height="480">
    Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
  </canvas>
  
  <h2>References</h2>
  <ul>
    <li><a href="http://en.wikipedia.org/wiki/Gouraud_shading">Gouraud shading</li>
    <li><a href="http://en.wikipedia.org/wiki/Phong_reflection_model">Phong reflection model</li>
    <li><a href="http://en.wikipedia.org/wiki/Shading#Directional_lighting">Directional lighting</li>
  </ul>
</body>
</html>